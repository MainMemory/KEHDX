//----------------Sonic CD Spinning Top Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
private alias object.value0 : Object.Rotation
private alias object.value1 : Object.OnObject
private alias object.value2 : Object.OriginYPos

// Player Alias
// WARNING: Variable VAR_PLAYERVALUE6 does not exist in RSDKv4!
private alias object.value5 : Player.MinRollSpeed

// Gravity
private alias 0 : GRAVITY_GROUND
private alias 1 : GRAVITY_AIR

// Stage SFX
private alias 1 : SFX_S_SHOT

private alias object.state : player.state
private alias object.xpos : player.xpos
private alias object.speed : player.speed
private alias object.frame : player.frame
private alias object.animation : player.animation
private alias object.gravity : player.gravity
private alias object.jumpPress : player.jumpPress
private alias object.up : player.up
private alias object.down : player.down
private alias object.left : player.left
private alias object.right : player.right

private alias object.value40 : player.hitboxLeft
private alias object.value38 : player.hitboxTop
private alias object.value41 : player.hitboxRight
private alias object.value39 : player.hitboxBottom

private alias object.value1 : player.timer
private alias ANI_WALKING : ANI_SPINNING_TOP


event ObjectUpdate
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
	
		BoxCollisionTest(C_SOLID, object.entityPos, -16, -8, 16, 24, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
	
	
		temp1 = object.rotation // This doesn't do anything...
		if checkResult == true
	
			if player[currentPlayer].state != Player_State_SpinningTop
				player[currentPlayer].speed = 0
				Object.OnObject = true
				Player[currentPlayer].MinRollSpeed = 0x1000
	
				if player[currentPlayer].xpos > object.xpos
					Player[currentPlayer].Timer = player[currentPlayer].xpos
					Player[currentPlayer].Timer -= object.xpos
	
					object.rotation = 128
				else
					Player[currentPlayer].Timer = object.xpos
					Player[currentPlayer].Timer -= player[currentPlayer].xpos
	
					object.rotation = 384
				end if
	
			end if
	
			if player[currentPlayer].xpos > object.xpos
				if player[currentPlayer].left == true
					Player[currentPlayer].Timer -= 0x1000
				end if
	
				if player[currentPlayer].right == true
					Player[currentPlayer].Timer += 0x1000
				end if
			else
				if player[currentPlayer].left == true
					Player[currentPlayer].Timer += 0x1000
				end if
	
				if player[currentPlayer].right == true
					Player[currentPlayer].Timer -= 0x1000
				end if
			end if
	
			player[currentPlayer].state = Player_State_SpinningTop
	
			player[currentPlayer].animation = ANI_SPINNING_TOP
	
			Sin(player[currentPlayer].xpos, object.rotation)
			player[currentPlayer].xpos *= Player[currentPlayer].Timer
			player[currentPlayer].xpos >>= 9
			player[currentPlayer].xpos += object.xpos
	
			temp0 = object.rotation
			temp0 -= 128
			temp0 /= 86
			player[currentPlayer].frame = temp0
	
			object.rotation += 16
			if object.rotation > 639
				object.rotation -= 512
			end if
			
			if player[currentPlayer].jumpPress == true
				
				player[currentPlayer].state = Player_Action_Jump
				player[currentPlayer].timer = 0
				
				player[currentPlayer].speed = player[currentPlayer].xpos
				player[currentPlayer].speed -= object.xpos
				player[currentPlayer].speed >>= 2

				object.ypos = Object.OriginYPos

				PlaySfx(SfxName[Shot], false)
			end if
		else
			if Object.OnObject == true
				if player[currentPlayer].state == Player_State_SpinningTop
					if player[currentPlayer].gravity == GRAVITY_GROUND
						player[currentPlayer].state = Player_State_Ground
						Object.OnObject = false
						Player[currentPlayer].Timer = 0
					end if
				end if
			end if
		end if
	next
end event


event ObjectDraw
	DrawSprite(object.frame)

	GetBit(temp0, ringTimer, 0)
	if temp0 == false
		object.frame++
		if object.frame > 2
			object.frame = 0
		end if
	end if
end event


event ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-16, -8, 32, 16, 101, 76) // #0 - Spin Top Frame 0
	SpriteFrame(-16, -8, 32, 16, 101, 76) // #1 - Spin Top Frame 1
	SpriteFrame(-16, -8, 32, 16, 134, 76) // #2 - Spin Top Frame 2

	foreach (TypeName[Spinning Top], arrayPos0, ALL_ENTITIES)
		Object[arrayPos0].OriginYPos = Object[arrayPos0].ypos
	next

end event


// ========================
// Editor Subs
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-16, -8, 32, 16, 101, 76) // #0 - Spin Top Frame 0


end event
