// ----------------------------------
// RSDK Project: Knuckles' Emerald Hunt
// Script Description: Chaos Emerald Object
// Script Author: MainMemory, KarlEmerald, Lave Slime
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer
private alias object.value1 : object.originPos.y

private alias 0 : EMERALD_NONE
private alias 1 : EMERALD_ITEM
private alias 2 : EMERALD_OBTAINED
private alias 3 : EMERALD_SPARKLE

// Emerald Manager (HUD) Aliases
private alias 0 : EMERALD_MODE_S2
private alias 1 : EMERALD_MODE_HPZ
private alias 2 : EMERALD_MODE_S1
private alias 3 : EMERALD_MODE_CD


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case EMERALD_NONE
		// (The object should never be in this state btw)
		Print("Invalid Emerald: ", false, false)
		Print(object.entityPos, true, true)
		break
		
	case EMERALD_ITEM //Basic State, floatin and movin
	
		//Makes it where object don't move
		if object.propertyvalue != 9
			Sin(temp0, object.angle)
			temp0 <<= 10
			object.ypos = object.originPos.y
			object.ypos += temp0
		end if
		
		object.angle += 4
		object.angle &= 511
		
		//cd time stone speed
		if EmeraldManager_Mode != EMERALD_MODE_CD
			object.frame++
			object.frame %= 96
		end if
		
		object.animationTimer++
		object.animationTimer &= 15
		
		// Check interaction with players
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			switch EmeraldManager_Mode 
			case EMERALD_MODE_HPZ
				// HPZ's got vertical emerald shards, which are just a tad bit larger
				BoxCollisionTest(C_TOUCH, object.entityPos, -8, -16, 8, 16, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
				break
			default
				//S1's and S2's emeralds are the same size 
				BoxCollisionTest(C_TOUCH, object.entityPos, -8, -8, 8, 8, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)			
				break
			case EMERALD_MODE_CD 
				//Time Stones are a bit larger
				BoxCollisionTest(C_TOUCH, object.entityPos, -10, -12, 10, 12, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)			
				break
			end switch
			
			//Did you touch it?
			//If so, you got it!!!
			if checkResult == true
				CallFunction(EmeraldManager_AwardEmerald)
				object.state = EMERALD_SPARKLE
				object.timer = 0
				object.frame = 0
			end if	
		next
		break
		
	case EMERALD_OBTAINED //the HUD makes a temp object using this state
	
		//Set Position
		currentPlayer = object.propertyValue
		object.xpos = object[currentPlayer].xpos
		object.ypos = object[currentPlayer].ypos
		object.ypos -= 0x261A80
		
		//Object Flashing
		object.timer++
		if object.timer < 150
			temp0 = object.timer
			temp0 &= 7
			switch temp0
			case 0
				object.alpha = 255
				break
			case 1
			case 7
				object.alpha = 192
				break
			case 2
			case 6
				object.alpha = 128
				break
			case 3
			case 5
				object.alpha = 64
				break
			case 4
				object.alpha = 0
				break
			end switch
		else
			//Goodbye...
			CallFunction(EmeraldManager_DeleteEmerald)
			ResetObjectEntity(object.entityPos, TypeName[Blank Object], 0, 0, 0)
		end if
		break
		
	case EMERALD_SPARKLE //Emerald has cute sparkles like Sonic 1
	
		//Change frame
		object.timer++
		if object.timer == 6
			object.timer = 0
			object.frame++
			if object.frame >= 4
				object.frame = 0
				object.value3++
			end if
		end if
		
		//Object gets deleted
		if object.value3 == 4
			ResetObjectEntity(object.entityPos, TypeName[Blank Object], 0, 0, 0)
		end if
		break

	end switch
end event


event ObjectDraw
	switch object.state
	case EMERALD_ITEM //Draws main emerald
		switch EmeraldManager_Mode
		case EMERALD_MODE_S2
		default
			DrawSprite(0)
			
			if object.frame >= 64
				temp0 = object.frame
				temp0 -= 60
				temp0 >>= 2
				DrawSprite(temp0)
			end if
			break
			
		case EMERALD_MODE_HPZ
			DrawSprite(9)
			break
			
		case EMERALD_MODE_S1
			if object.animationTimer < 8
				DrawSprite(10)
			else
				DrawSprite(11)
			end if
			break
			
		case EMERALD_MODE_CD
			temp0 = object.frame
			temp0 /= 12
			object.frame++
			if object.frame > 47
				object.frame = 0
			endif
			temp0 += 12
			DrawSprite(temp0)
			break

		end switch
		break
		
	case EMERALD_OBTAINED //Draws emerald when obtained, only one frame
		switch EmeraldManager_Mode
		case EMERALD_MODE_S2
		default
			temp0 = 0
			break
		case EMERALD_MODE_HPZ
			temp0 = 9
			break
		case EMERALD_MODE_S1
			temp0 = 10
			break
		case EMERALD_MODE_CD
			temp0 = 12
			break
		end switch
		
		DrawSpriteFX(temp0, FX_INK, object.xpos, object.ypos)
		break
		
	case EMERALD_SPARKLE // cute sparkles
		temp0 = 16
		temp0 += object.frame
		DrawSprite(temp0)
		break

	end switch
end event


event ObjectStartup
	LoadSpriteSheet("Global/Items.gif")
	
	if specialMode == false
		// 0: S2 Chaos Emerald, full
		SpriteFrame(-8, -8, 16, 16, 109, 158)
		
		// 1 - 8: S2 Chaos Emerald, shine frames
		temp0 = 175
		while temp0 < 193
			temp1 = 109
			while temp1 < 161
				SpriteFrame(-8, -8, 16, 16, temp1, temp0)
				temp1 += 17
			loop
			temp0 += 17
		loop
		
		// 9: HPZ Shard
		SpriteFrame(-8, -16, 16, 32, 178, 159)
		
		// 10 - 11: S1 Emerald
		SpriteFrame(-8, -8, 16, 16, 126, 158)
		SpriteFrame(-8, -8, 16, 16, 143, 158)

		//12 - 15: CD Time Stone
		temp0 = 0
		temp1 = 109
		while temp0 < 4
			SpriteFrame(-10, -12, 20, 24, temp1, 209)
			temp1 += 21
			temp0++
		loop
	else
		// 0 - 11, make them all the same sprite
		temp0 = 0
		while temp0 < 16
			SpriteFrame(-8, -8, 16, 16, 177, 192)
			temp0++
		loop
	end if
	
	//16 - 19, Emerald Sparkle
	SpriteFrame(-12, -12, 24, 24, 206, 149)
	SpriteFrame(-12, -12, 24, 24, 231, 149)
	SpriteFrame(-12, -12, 24, 24, 206, 174)
	SpriteFrame(-12, -12, 24, 24, 231, 174)

	foreach (TypeName[Chaos Emerald], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].drawOrder = 4
		object[arrayPos0].originPos.y = object[arrayPos0].ypos
		object[arrayPos0].inkEffect = INK_BLEND
		CallFunction(EmeraldManager_AddEntity) 
	next
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Items.gif")
	SpriteFrame(-8, -8, 16, 16, 109, 158)
	
	// KEHDX TODO: just like it says, prop vals seem to be rather arbitrary?
	// i lack the knowledge to do this myself, best to leave it to someone else...
	SetVariableAlias(ALIAS_VAR_PROPVAL, "idkTbh")
end event
