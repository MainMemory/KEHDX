// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Sega Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer
private alias object.value2 : object.trailPos.x
private alias object.value3 : object.levelSelectCheatPos
private alias object.value4 : object.anyCheatDone
private alias object.value5 : object.regionSwitchCheatPos

// KEHDX values

// A pair of screen X values, configured at startup
private alias object.value6 : object.e.l
private alias object.value7 : object.e.r

// States
private alias 0 : SEGA_SETUP
private alias 1 : SEGA_FADEIN
private alias 2 : SEGA_SONICRUN_LEFT
private alias 3 : SEGA_FADEOUTTRAIL_LEFT
private alias 4 : SEGA_SONICRUN_RIGHT
private alias 5 : SEGA_FADEOUTTRAIL_RIGHT
private alias 6 : SEGA_SHOWSEGALOGO
private alias 7 : SEGA_FADETOTITLE

// SaveRAM Aliases	
public alias saveRAM[202] : saved.character

// Menu Boot Mode Aliases
// Public since the following Menu-related objects use them too
public alias -1 : BOOTMODE_NONE
public alias  1 : BOOTMODE_MAINMENU
public alias  2 : BOOTMODE_OPTIONS
public alias  3 : BOOTMODE_STAGESELECT
public alias  4 : BOOTMODE_COMPETITION

// KEHDX Cheat, to enable/disable the splashes (SEGA, SHC)
private alias 0 : KEHDX_DISABLESPLASHES


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case SEGA_SETUP
		object.alpha 		= 0x00
		object.inkEffect 	= 2
		object.timer 		= 320
		SetScreenFade(0x00, 0x00, 0x00, object.timer)
		
		// Set up the bounds
		object.e.l = screen.xcenter
		object.e.l -= 50
		object.e.r = object.e.l
		object.e.r += 42
		
		object.state++
		break

	case SEGA_FADEIN
		if object.timer > 0
			object.timer -= 8
		else
			object.direction 	= 1
			object.alpha 		= 0x100
			object.trailPos.x 	= 256
			object.state++
		end if
		SetScreenFade(0x00, 0x00, 0x00, object.timer)
		break

	case SEGA_SONICRUN_LEFT
		object.frame++
		object.frame &= 3

		if object.trailPos.x > -320
			object.trailPos.x -= 32
		else
			object.direction = FACING_RIGHT
			object.state++
		end if
		break

	case SEGA_FADEOUTTRAIL_LEFT
		if object.alpha > -0x40
			object.alpha -= 0x10
		else
			object.state++
		end if
		break

	case SEGA_SONICRUN_RIGHT
		object.frame++
		object.frame &= 3

		if object.trailPos.x < 320
			object.trailPos.x += 32
		else
			object.alpha = 0x100
			object.state++
		end if
		break

	case SEGA_FADEOUTTRAIL_RIGHT
		if object.alpha > -0x40
			object.alpha -= 0x10
		else
			object.state++
		end if
		break

	case SEGA_SHOWSEGALOGO
		object.timer++
		if object.timer == 160
			object.timer = 0
			object.state = 7
		end if
		
		if object.timer == 2
			switch saved.character 
			case 0
				PlaySfx(SfxName[Sega Chant], false)
				break
			case 1
				PlaySfx(SfxName[Tikal Chant], false)
				break
			end switch
		end if
		
		// Checking for the box around the second letter
		CheckTouchRect(object.e.l, 90, object.e.r, 150)
		if checkResult > -1
			specialMode = true
			
			// Crop it up
			EditFrame(11, -51, -29, 44, 58, 341, 192)
		end if
		break

	case SEGA_FADETOTITLE
		if object.timer < 0x100
			object.timer += 8
			SetScreenFade(0x00, 0x00, 0x00, object.timer)
		else
			ResetObjectEntity(object.entityPos, TypeName[SHC Splash], 0, object.xpos, object.ypos)
			SetScreenFade(0x00, 0x00, 0x00, 0xFF)
		end if
		break
		
	end switch
end event


event ObjectDraw
	DrawRect(0, 0, screen.xsize, screen.ysize, 0xE0, 0xE0, 0xE0, 0xFF)

	switch object.state
	default
	case SEGA_SETUP
	case SEGA_FADEIN
		break

	case SEGA_SONICRUN_LEFT
		temp0 = object.trailPos.x
		temp0 += screen.xcenter
		DrawSpriteScreenFX(4, FX_FLIP, temp0, 121)
		DrawSpriteScreenFX(5, FX_FLIP, temp0, 121)
		DrawSpriteScreenFX(6, FX_FLIP, temp0, 121)
		DrawSpriteScreenFX(object.frame, FX_FLIP, temp0, screen.ycenter)
		break

	case SEGA_FADEOUTTRAIL_LEFT
		DrawSpriteScreenFX(7, FX_INK, 0, 121)
		DrawSpriteScreenFX(8, FX_INK, 0, 121)
		DrawSpriteScreenFX(9, FX_INK, 0, 121)

		temp0 = object.alpha
		object.alpha = 256
		object.alpha -= temp0
		DrawSpriteScreenFX(10, FX_INK, screen.xcenter, screen.ycenter)

		object.alpha = temp0
		break

	case SEGA_SONICRUN_RIGHT
		temp0 = object.trailPos.x
		temp0 += screen.xcenter
		DrawSpriteScreenFX(4, FX_FLIP, temp0, 120)
		DrawSpriteScreenFX(5, FX_FLIP, temp0, 120)
		DrawSpriteScreenFX(6, FX_FLIP, temp0, 120)
		DrawSpriteScreenXY(10, screen.xcenter, screen.ycenter)

		DrawSpriteScreenFX(object.frame, FX_FLIP, temp0, screen.ycenter)
		break

	case SEGA_FADEOUTTRAIL_RIGHT
		DrawSpriteScreenFX(7, FX_INK, 0, 120)
		DrawSpriteScreenFX(8, FX_INK, 0, 120)
		DrawSpriteScreenFX(9, FX_INK, 0, 120)
		// [Fallthrough]
	case SEGA_SHOWSEGALOGO
	case SEGA_FADETOTITLE
		DrawSpriteScreenXY(11, screen.xcenter, screen.ycenter)
		break
		
	end switch
end event


event ObjectStartup
	LoadSpriteSheet("KEHMenu/Menu.gif")
	
	// Use the player sprites of the last character played with
	if saved.character == 0
		// Knuckles's Running Sprites
		SpriteFrame(-31, -36, 62, 72, 288, 113)
		SpriteFrame(-31, -36, 62, 72, 351, 113)
		SpriteFrame(-29, -36, 60, 72, 414, 113)
		SpriteFrame(-31, -36, 62, 72, 16, 68)
	else
		// Tikal's Running Sprites
		SpriteFrame(-31, -36, 62, 72, 257, 260)
		SpriteFrame(-31, -36, 62, 72, 320, 260)
		SpriteFrame(-29, -36, 60, 72, 383, 260)
		SpriteFrame(-31, -36, 62, 72, 444, 260)
	end if
	
	// Smear Frames
	SpriteFrame(-192, -35, 256, 70, 256, 398)
	SpriteFrame(-384, -35, 192, 70, 256, 398)
	SpriteFrame(-576, -35, 192, 70, 256, 398)
	SpriteFrame(0, -35, 192, 70, 256, 398)
	SpriteFrame(192, -35, 192, 70, 256, 398)
	SpriteFrame(384, -35, 192, 70, 256, 398)
	
	// SEGA Frames
	SpriteFrame(-93, -29, 187, 58, 112, 192)
	SpriteFrame(-93, -29, 187, 58, 299, 192) // Do note - if you change this, make sure to update the use of EditFrame in SEGA_SHOWSEGALOGO too!
	
	// Hide Touch overlay, and default to Physical controls active initially instead
	options.touchControls = false
	options.physicalControls = true
	
#platform: USE_ORIGINS
	if game.titleMode != false
		menu.bootMode = BOOTMODE_MAINMENU
	end if
	
	game.titleMode = false
#endplatform
	
	switch menu.bootMode
	case BOOTMODE_NONE
	default
		// Nothing special needed here, player just booted up the game so give them all the splashes
		temp0 = TypeName[Sega]
		
		if KEHDX_DISABLESPLASHES == true
			// Unless y'know, you don't want 'em
			
			temp0 = TypeName[Main Menu]
			menu.bootMode = BOOTMODE_MAINMENU
		end if
		break
		
	case BOOTMODE_MAINMENU
		temp0 = TypeName[Main Menu]
		break
		
	case BOOTMODE_OPTIONS
		temp0 = TypeName[Options]
		break
		
	case BOOTMODE_STAGESELECT
		temp0 = TypeName[Stage Select]
		break
		
	case BOOTMODE_COMPETITION
		if engine.onlineActive == true
			temp0 = TypeName[Competition L1]
		else
			// TODO: online ver of menu goes here
			temp0 = TypeName[Competition L1]
		end if
		break
		
	end switch
	
	foreach (TypeName[Sega], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].type = temp0
		object[arrayPos0].priority = 1
	next
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("KEHMenu/Menu.gif")
	SpriteFrame(-31, -36, 62, 72, 288, 113)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
