// ----------------------------------
// RSDK Project: Knuckles' Emerald Hunt
// Script Description: Fall Start Object
// Script Author: Lave sIime
// ----------------------------------

// This object is used to make the player start a level falling
// Not too much else to say...
// It's normally used in LZ and HPZ, though it can be used in other zones too if desired

// ========================
// Aliases
// ========================

// Player Aliases
private alias object.state     : player.state
private alias object.direction : player.direction
private alias object.animation : player.animation
private alias object.gravity   : player.gravity


// ========================
// Events
// ========================

event ObjectUpdate
	if object.propertyValue == 0xFF
		// The Master object, as placed in a level
		
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			CreateTempObject(object.type, currentPlayer, object.xpos, object.ypos)
			object[tempObjectPos].direction = object.direction
		next
		
		object.type = TypeName[Blank Object]
	else
		// The individual, per-player object, as spawned by the Master
		
		currentPlayer = object.propertyValue

		if player[currentPlayer].gravity == GRAVITY_GROUND
			object.type = TypeName[Blank Object]
		else
			player[currentPlayer].direction = object.direction
			player[currentPlayer].animation = ANI_HURT
		end if
	end if
end event


// ========================
// Editor Events
// ========================

event RSDKEdit
	if editor.returnVariable == true
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			checkResult = object.propertyValue
			break
			
		case 0 // direction
			checkResult = object.direction
			checkResult &= 1
			break
			
		end switch
	else
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			// lock it to Master type, when placed in a scene
			object.propertyValue = 0xFF
			break
			
		case 0 // direction
			object.direction = editor.variableValue
			object.direction &= 1
			break
			
		end switch
	end if
end event


event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-8, -8, 16, 16, 168, 18)
	
	AddEditorVariable("direction")
	SetActiveVariable("direction")
	AddEnumVariable("Left", 0)
	AddEnumVariable("Right", 1)
	
	// As placed in a scene, prop val should always be 0xFF
	SetVariableAlias(ALIAS_VAR_PROPVAL, "isMaster")
end event
